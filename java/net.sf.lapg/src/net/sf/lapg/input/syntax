#   syntax: lalr1 generator source grammar
#
#   Lapg (Lexical Analyzer and Parser Generator)
#   Copyright (C) 2002-06  Evgeny Gryaznov (inspirer@inbox.ru)
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

.lang        "java" 
.getsym      "chr = l < buff.length ? buff[l++] : 0"
.class       "Parser"
.namespace	 "net.sf.lapg.input"
.maxtoken    4096
.breaks		 "on"
.lexemend    "on"
.positioning "offset"

# Vocabulary

[0]

identifier(String):     /[a-zA-Z_][a-zA-Z_0-9]*|'[^'\n]+'/  { @ = new String(token,0,lapg_size); break; }
regexp(String):	/\/([^\/\\]|\\.)*\//	{ @ = new String(token,1,lapg_size-2); break; }
scon(String):	/"[^"\n\\]*"/			{ @ = new String(token,1,lapg_size-2); break; }
type(String):	/\([^\)\n]+\)/			{ @ = new String(token,1,lapg_size-2); break; }
icon(Integer):	/-?[0-9]+/				{ @ = Integer.parseInt(new String(token,0,lapg_size)); break; }
eoi:           /\n%%.*/
'%':           /\n%/
_skip:         /\n|[\t\r ]+/    		{ continue; }
_skip:  /#.*/
'::=':  /::=/
'|':    /\|/
';':    /;/
'.':    /\./
':':    /:/
'[':    /\[/
']':    /\]/
'<<':   /<</

'{':	/{/		{ deep = 1; group = @1; break; }

[1]

_skip:	/'([^\n\\']|\\(['"?\\abfnrtv]|x[0-9a-fA-F]+|[0-7]([0-7][0-7]?)?))*'/
_skip:	/"([^\n\\"]|\\(['"?\\abfnrtv]|x[0-9a-fA-F]+|[0-7]([0-7][0-7]?)?))*"/
_skip:	/[^'"{}]+/
'i{':	/{/				{ deep++; break; }
'}':	/}/				{ if( --deep == 0 ) group = @0; break; }

# Attributes

[]

# Grammar

input ::=
	directives
	lexical_definitions
	grammar_definitions ;

directives ::= 
	directives directive | directive ;

directive ::=
	'.' identifier scon 
	| '.' identifier icon 
;

lexical_definitions ::= 
	lexical_definitions lexical_definition 
	| lexical_definition 
;

lexical_definition ::=
	'[' iconlist ']'
	| def_symbol typeopt ':'
	| def_symbol typeopt ':' regexp iconopt commandopt
;

iconlist(Integer) ::=
	icon | iconlist icon ;

grammar_definitions ::=
	grammar_definition | grammar_definitions grammar_definition ;

grammar_definition ::= 
	def_symbol typeopt '::=' rule_right_part ';'
	| '%' identifier identifiers ';' 
; 

rule_right_part (List<CRule>) ::= 
	rule_right_part '|' rule_def
	| rule_def
;

identifiers ::= 
	identifier | identifiers identifier ;

rule_def (CRule) ::=
	rule_symbols commandopt rule_priorityopt ;

rule_symbols (List<CSymbol>) ::=
	rule_symbols commandopt symbol
	| commandopt symbol
;

rule_priority (CSymbol) ::=
	'<<' symbol											{ $$ = $symbol; } 
;

command (CAction) ::=
	'{' command_tokens '}'								{ $$ = new CAction(@0.line, rawData(@0.offset,@~0.offset)); }  
;
	
command_tokens ::=
	command_tokens command_token | command_token ;

command_token ::=
	'i{' command_tokensopt '}' 
;

symbol (CSymbol) ::=
	identifier											{ $$ = getSymbol($identifier,-1); }
;

def_symbol (CSymbol) ::=
	identifier											{ $$ = getSymbol($identifier,@0.line); }
;

##################################################################################

%%
import java.io.UnsupportedEncodingException;
import java.text.MessageFormat;
import java.util.ArrayList;
import java.util.HashMap;
import java.util.List;
%%

public Parser() {
}

private static final boolean DEBUG_SYNTAX = false;

private HashMap<String,CSymbol> symbols = new HashMap<String,CSymbol>();

private byte[] buff;
private int l;
private int deep = 0;

private String rawData(int start, int end) {
	return new String(buff, start, end-start);
}

private CSymbol getSymbol(String name, int line) {
	CSymbol res = symbols.get(name);
	if( res == null ) {
		res = new CSymbol(name);
		symbols.put(name,res);
	}
	if( line > 0 ) {
		res.intDefine(line);
	}
	return res;
}

private CSymbol wrapCommand(CAction action) {
	return new CSymbol(null); // TODO
}

void error( String s ) {
	System.err.println(s);
}

public boolean parse(String s) {
	l = 0;
	try {
		buff = s.getBytes("utf-8");
	} catch( UnsupportedEncodingException ex ) {
		return false;
	}
	boolean res = parse();
	return res;
}
